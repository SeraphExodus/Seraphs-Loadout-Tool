include library.smuggler;
include library.static_item;
include library.utils;

const string_id SID_OPEN_CRATE  = 	new string_id( "spam", "open_crate" );

trigger OnObjectMenuRequest(obj_id player, menu_info mi)
{
	mi.addRootMenu (menu_info_types.ITEM_USE, new string_id(SID_OPEN_CRATE));
	return SCRIPT_CONTINUE;
}

trigger OnObjectMenuSelect(obj_id player, int item)
{
	if(!isIdValid(player) || !isIdValid(self))
	{
		return SCRIPT_CONTINUE;
	}
	
	if (item == menu_info_types.ITEM_USE)
	{
		string LOOT_LIST = ""; //this should *NOT* have been a global var...
		int difficulty = getIntObjVar(self, "difficulty");
		int minFreeSpace = difficulty * 3;	//new Var to change the freeSpace lookup to be dynamic - Karthoris
		if(difficulty <= 0 || difficulty > 6) //Updated - Karthoris
		{
			// Invalid difficulty
			return SCRIPT_CONTINUE;
		}

		if(difficulty == 6) //increase Inv check size and set the LOOT_LIST for Tier 6 Convoys - Karthoris
		{
			minFreeSpace += 3; 
			LOOT_LIST =	"datatables/space_loot/piracy_loot/ht_group_crate_list.iff";
		}
		else //Added else to catch the LOOT_LIST var getting stuck on the wrong Datatable
		{
			LOOT_LIST = "datatables/space_loot/piracy_loot/crate_list.iff"; //set loot_list back to standard - otherwise it gets stuck on ht_group
		}
		
		//sendSystemMessage(player, "LOOT LIST " + LOOT_LIST, null); //Debug - Karthoris
		
		obj_id inv = utils.getInventoryContainer(player);
		
		if(utils.checkConfigFlag("GameServer", "legendsAnniversary"))
			minFreeSpace += 1;

		int freeSpace = getVolumeFree(inv);
		if(freeSpace < minFreeSpace) //changed to use the new variable - Karthoris
		{
			sendSystemMessage(player, "You must have room for at least " + minFreeSpace + " items in your inventory to open this container.", null);
			return SCRIPT_CONTINUE;
		}		
		
		createLoot(self, player, difficulty, LOOT_LIST);
	}
	return SCRIPT_CONTINUE;
}

void createLoot(obj_id self, obj_id player, int difficulty, string LOOT_LIST)
{
	obj_id inventory = utils.getInventoryContainer(player);
	int itemCount = ((difficulty + rand(1, 2)) * 2); 
	
//----------------Added Modifier so Level of the Convoy adjusts the loot so higher Lvls give better loot - Karthoris------------//
	int difficultyModifier = difficulty * 5; // Added - Karthoris
	
	for(int i=0; i<itemCount; i++ )
	{
		int lootRoll = rand(1, 100);
		if(lootRoll > 0 && lootRoll <= (60 - difficultyModifier))
		{
			// Generate regular item
			string regItem = generateRegularItem(player, LOOT_LIST);
			if(regItem.length() > 0)
			{
				createObject(regItem, inventory, "");
			}
		}
		
		if(lootRoll > (60  - difficultyModifier) && lootRoll <= (65 - difficultyModifier))
		{
			// Flight plans
			string flightPlan = generateRareItem(player, "flightPlan", LOOT_LIST);
			if(flightPlan.length() > 0)
			{
				static_item.createNewItemFunction(flightPlan, inventory);
			}
		}

		if(lootRoll > (65 - difficultyModifier) && lootRoll <= (80 - difficultyModifier))
		{
			// Reward items
			string rewardItem = generateRareItem(player, "rewardItems", LOOT_LIST);
			if(rewardItem.length() > 0)
			{
				createObject(rewardItem, inventory, "");
			}
		}

		if(lootRoll > (80 - difficultyModifier) && lootRoll <= (88 - difficultyModifier))
		{
			// Generate rare schematic (weapons & elite engine)
			string schematicItem = generateRareItem(player, "schematics", LOOT_LIST);
			if(schematicItem.length() > 0)
			{
				static_item.createNewItemFunction(schematicItem, inventory);
			}
		}

		if(lootRoll > (88 - difficultyModifier))
		{
			// Generate level 6 / 10 engines
			string rareItem = generateRareItem(player, "rareItems", LOOT_LIST);
			if(rareItem.length() > 0)
			{
				createObject(rareItem, inventory, "");
			}
		}
	}

//----------------------------------------------------------------------------------------------------//
//------------------- Added Deco Items Loot Creation for Tier 6 Convoys - Karthoris ------------------//

	if (difficulty == 6)
	{
		// -------- FOR PROD --------//
		int decoRand = rand(1, 100);
		if(decoRand > 80 || utils.isTestServer())
		{
			// Get the type of Deco and create it
			string decoItem = generateRareItem(player, "decoItems", LOOT_LIST);
			createObject(decoItem, inventory, "");
		}	
	}

//----------------------------------------------------------------------------------------------------//

	if(utils.isProfession(player, utils.SMUGGLER))
	{
		int smugglerItems = rand(1, 5);
		for(int i=0; i<smugglerItems; i++ )
		{
			int smugglerRoll = rand(1, 100);
			if(smugglerRoll <= 50)
			{
				int smuggDifficulty = difficulty; // Had to cap this at 5, Smuggler Tiers only go to 5 - Karthoris
				if (smuggDifficulty > 5)
					smuggDifficulty = 5;
				smuggler.createRandomContrabandTier(player, smuggDifficulty);
			}
			
			if(smugglerRoll > 50)
			{
				obj_id module = utils.getStaticItemInInventory(player, "item_reward_modify_pistol_01_01");

				if(isIdValid(module))
				{
					incrementCount(module, rand(1, 2));
				}
				else
				{
					static_item.createNewItemFunction("item_reward_modify_pistol_01_01", player);
					module = utils.getStaticItemInInventory(player, "item_reward_modify_pistol_01_01");
					incrementCount(module, rand(0, 1));
				}		
			}
		}
		int blackhandRoll = rand(1, 100);
		if(blackhandRoll <= difficulty)
		{
			static_item.createNewItemFunction("weapon_smuggler_blackhand", player);
		}
	}
	
	//McSquizzy 20200221
	//add legends anniversary loot to the Piracy Containers
	if(utils.checkConfigFlag("GameServer", "legendsAnniversary"))
	{
		//if(difficulty < 6)
			//static_item.createNewItemFunction("item_cts_galaxy_painting", player);
		//else
		if(difficulty == 6)
			static_item.createNewItemFunction("item_tcg_loot_reward_series5_trench_run_diorama", player);
	}
	
	destroyObject(self);
}	

string generateRegularItem(obj_id player, string LOOT_LIST)
{
	// Get the type of component
	string[] compType = dataTableGetStringColumnNoDefaults(LOOT_LIST, "componentType");
	if(compType == null)
	{	
		return null;
	}
	int realCompTypeLength = 0;
	for(int i = 0; i < compType.length; i++)
	{
		if(compType[i] != null && compType[i].length() > 0)
		{

			realCompTypeLength++;
		}
	}
	
	string component = compType[rand(0, realCompTypeLength -1)];
	
	// Pick an item level
	int level = rand(5, 10); //Modified general Part drop to be Levels 5 - 10 Only - Karthoris
	string col = "level"+level;
	
	string table = "datatables/item/vendor/space_duty/"+component+".iff";
	string[] compList = dataTableGetStringColumnNoDefaults(table, col);	
	
	if(compList == null)
	{	
		return null;
	}
	
	string item = compList[rand(0, (compList.length -1))];
	return item;
}

string generateRareItem(obj_id player, string type, string LOOT_LIST)
{
	// Get the type of component
	string[] compList = dataTableGetStringColumnNoDefaults(LOOT_LIST, type);
	if(compList == null)
	{	
		return null;
	}
	string component = compList[rand(0, (compList.length -1))];
	return component;
}